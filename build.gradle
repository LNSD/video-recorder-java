plugins {
  id 'idea'
  id 'java'
  id 'com.bmuschko.nexus' version '2.3.1'
  id 'com.adarshr.test-logger' version '2.1.1'
}

allprojects {
  repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
  }
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven'
  apply plugin: 'com.bmuschko.nexus'
  apply plugin: 'signing'
  apply plugin: 'com.adarshr.test-logger'

  archivesBaseName = baseArchiveName + '-' + project.name

  sourceCompatibility = 1.8

  modifyPom {
    project {
      name 'com.automation.remarks.video-recorder'
      packaging 'jar'
      description 'Video Recorder is a library that allows to record selenium tests'
      url 'https://github.com/SergeyPirogov/VideoRecorder'

      scm {
        url 'scm:git@github.com:SergeyPirogov/VideoRecorder.git'
        connection 'scm:git@github.com:SergeyPirogov/VideoRecorder.git'
        developerConnection 'scm:git@github.com:SergeyPirogov/VideoRecorder.git'
      }

      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'SerhiiP'
          name 'Sergey Pirogov'
        }
      }
    }
  }

  extraArchive {
    sources = true
    tests = false
    javadoc = true
  }

  nexus {
    sign = true
  }

  test {
    // Disable the test report for the individual test task
    reports.html.enabled = false
  }
}

testlogger {
  theme 'mocha'
  slowThreshold 5000
  showFullStackTraces true
}

task testReport(type: TestReport) {
  // make sure this task is run after all subproject test tasks
  mustRunAfter subprojects*.test

  destinationDir = file("${buildDir}/reports/allTests")
  // Include the results from the `test` task in all subprojects
  reportOn subprojects*.test
}

test.finalizedBy 'testReport'
